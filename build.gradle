plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

ext {
	vertxVersion = '4.2.1'
}

subprojects {
	apply plugin: 'java-library'     // 加入该插件才能在 gradle 中使用 api
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'maven-publish'

	repositories {
		mavenCentral()
	}

	java {
		withSourcesJar()
	}

	jar {
		archiveClassifier = ''
	}

	bootJar {
		enabled = false
	}

	group = 'org.jd.zeus'
	version = '0.0.1-SNAPSHOT'

	dependencyManagement {
		dependencies {
			dependency "io.vertx:vertx-core:${vertxVersion}"
			dependency "io.vertx:vertx-web:${vertxVersion}"
			dependency "io.vertx:vertx-web-client:${vertxVersion}"
			dependency "io.vertx:vertx-micrometer-metrics:${vertxVersion}"

			dependency 'com.google.guava:guava:31.0.1-jre'
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
	}

	publishing {
		publications {
			maven(MavenPublication) {
				from components.java
				versionMapping {
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
			}
		}
		repositories {
			mavenLocal()
		}
	}

}